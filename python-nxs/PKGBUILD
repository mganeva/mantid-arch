# Maintainer: Ross Whitfield <whitfieldre@ornl.gov>
pkgname=('python-nxs-git' 'python2-nxs-git')
pkgver=r101.0add4b7
pkgrel=1
pkgdesc="Python bindings for NAPI"
url="https://github.com/nexusformat/python-nxs"
arch=("any")
license=('LGPLv2')
makedepends=('python-setuptools')
source=("git+https://github.com/nexusformat/python-nxs.git" "python3.patch")
md5sums=('SKIP' '9b54be4a09d5d001d7a75f93a43a2606')

pkgver() {
    cd $srcdir/python-nxs
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cp -a "${srcdir}/python-nxs"{,-py2}
    cd ${srcdir}/python-nxs
    patch -Np1 -i "${srcdir}/python3.patch"
}

package_python-nxs-git() {
    depends=('python-numpy')
    provides=('python-nxs')
    cd "$srcdir/python-nxs"
    python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-nxs-git() {
    depends=('python2-numpy')
    provides=('python2-nxs')
    cd "$srcdir/python-nxs-py2"
    python2 setup.py install --root="$pkgdir/" --optimize=1
}
